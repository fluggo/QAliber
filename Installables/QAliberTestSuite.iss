; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{3A880920-8CCB-4847-A1BD-A97644FD18B3}
AppName=QAliber Test Suite
AppVerName=QAliber Test Suite 1.0
AppPublisher=QAlibers (c)
AppPublisherURL=http://www.qaliber.net/
AppSupportURL=http://www.qaliber.net/
AppUpdatesURL=http://www.qaliber.net/
CreateAppDir=no
LicenseFile=..\QAliber Test Builder\Presentation\COPYING.txt
OutputBaseFilename=QAliberTestSuiteSetup
Compression=lzma
SolidCompression=yes
WizardImageFile=QAliberSetupLogo.bmp
WizardSmallImageFile=QAliberLogo55.bmp

[Types]
Name: "full"; Description: "Full installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "installvs2005"; Description: "Install Visual Studio 2005 Plug-in (QAlibet Test Developer)"; Types: full custom; Check: IsVS2005Installed
Name: "installvs2008"; Description: "Install Visual Studio 2008 Plug-in (QAlibet Test Developer)"; Types: full custom; Check: IsVS2008Installed
Name: "installdevsa"; Description: "Install Standalone QAlibet Test Developer (For use with VS express editions)"; Types: full custom; Check: IsVSNotInstalled
Name: "installbuilder"; Description: "Install QAliber Test Builder"; Types: full custom;


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
;QAliber shared files
Source: "..\Binaries\QAliber.Engine.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "QAliber.Engine, Version=1.0.0.0, Culture=neutral, PublicKeyToken=69C52B3DB892D621, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\QAliber.TestModel.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "QAliber.TestModel, Version=1.0.0.0, Culture=neutral, PublicKeyToken=AAC3B2E748180B41, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\QAliber.RemotingModel.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "QAliber.RemotingModel, Version=1.0.0.0, Culture=neutral, PublicKeyToken=7CA69BFCD2253FEF, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\QAliber.Logger.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "QAliber.Logger, Version=1.0.0.0, Culture=neutral, PublicKeyToken=C4B865E92B70ED57, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\QAliber.ImageHandling.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "QAliber.ImageHandling, Version=1.0.0.0, Culture=neutral, PublicKeyToken=69C52B3DB892D621, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\QAliberManagedInjector.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "QAliberManagedInjector, Version=1.0.0.0, Culture=neutral, PublicKeyToken=F7C709BBC167CB3E, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
;QAliber CV dependencies
Source: "..\Binaries\Emgu.CV.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "Emgu.CV, Version=2.0.1.0, Culture=neutral, PublicKeyToken=7281126722AB4438, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\Emgu.Util.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "Emgu.Util, Version=2.0.0.0, Culture=neutral, PublicKeyToken=7281126722AB4438, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
;QAliber other dependencies
Source: "..\Binaries\ManagedWinapi.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "ManagedWinapi, Version=0.3.0.0, Culture=neutral, PublicKeyToken=41EB0F6B4CCD675B, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\Microsoft.mshtml.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "Microsoft.mshtml, Version=7.0.3300.0, Culture=neutral, PublicKeyToken=B03F5F7F11D50A3A"; Flags: gacinstall sharedfile
Source: "..\Binaries\ZedGraph.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "ZedGraph, Version=5.1.5.28844, Culture=neutral, PublicKeyToken=02A83CBD123FCD60, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
Source: "..\Binaries\tessnet2_32.dll"; DestDir: "{cf}\QAliber"; StrongAssemblyName: "tessnet2_32, Version=2.0.4.0, Culture=neutral, PublicKeyToken=1550524B5BA07E29, ProcessorArchitecture=MSIL"; Flags: gacinstall sharedfile
;QAliber CV unmanaged dependencies
Source: "..\Binaries\cv200.dll"; DestDir: "{sys}";
Source: "..\Binaries\cxcore200.dll"; DestDir: "{sys}";
;QAliber Documentation
Source: "..\Binaries\*.XML"; DestDir: "{cf}\QAliber"

Source: "QAliberTestBuilderSetup.msi"; DestDir: "{win}"; Flags: ignoreversion deleteafterinstall
Source: "QAliberVS2005PluginSetup.msi"; DestDir: "{win}"; Flags: ignoreversion deleteafterinstall; Check: IsVS2005Installed
Source: "QAliberVS2008PluginSetup.msi"; DestDir: "{win}"; Flags: ignoreversion deleteafterinstall; Check: IsVS2008Installed
Source: "QAliberDeveloperStandaloneSetup.msi"; DestDir: "{win}"; Flags: ignoreversion deleteafterinstall; Check: IsVSNotInstalled
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Run]
Filename: "msiexec"; Parameters: "/i ""{win}\QAliberTestBuilderSetup.msi"""; WorkingDir: "{win}"; StatusMsg: "Installing QAliber Test Builder..."; Check: IsComponentSelected('installbuilder')
Filename: "msiexec"; Parameters: "/i ""{win}\QAliberVS2005PluginSetup.msi"""; WorkingDir: "{win}"; StatusMsg: "Installing VS 2005 Plug-in..."; Check: IsComponentSelected('installvs2005')
Filename: "msiexec"; Parameters: "/i ""{win}\QAliberVS2008PluginSetup.msi"""; WorkingDir: "{win}"; StatusMsg: "Installing VS 2008 Plug-in..."; Check: IsComponentSelected('installvs2008')
Filename: "msiexec"; Parameters: "/i ""{win}\QAliberDeveloperStandaloneSetup.msi"""; WorkingDir: "{win}"; StatusMsg: "Installing Standalone QAliber Developer..."; Check: IsComponentSelected('installdevsa')

[UnInstallRun]
Filename: "msiexec"; Parameters: "/x {{20D197D0-8E7B-42A5-B58E-8E510350F352}"; WorkingDir: "{win}"; StatusMsg: "Un-installing QAliber Test Builder..."; Check: IsComponentSelected('installbuilder')
Filename: "msiexec"; Parameters: "/x {{5CD9EE21-8C36-45BD-93AC-B090D2ED7A8F}"; WorkingDir: "{win}"; StatusMsg: "Un-installing VS 2005 Plug-in..."; Check: IsComponentSelected('installvs2005')
Filename: "msiexec"; Parameters: "/x {{105E14C1-C2C6-486F-81B0-3217DFDA1086}"; WorkingDir: "{win}"; StatusMsg: "Un-installing VS 2008 Plug-in..."; Check: IsComponentSelected('installvs2008')
Filename: "msiexec"; Parameters: "/x {{D07EB533-D87F-4736-9A87-217A81AACA09}"; WorkingDir: "{win}"; StatusMsg: "Un-installing Standalone QAliber Developer..."; Check: IsComponentSelected('installdevsa')

[Code]
function IsVS2005Installed() : Boolean;
begin
   Result := RegValueExists(HKEY_LOCAL_MACHINE, 'Software\Microsoft\VisualStudio\8.0', 'InstallDir') or RegValueExists(HKEY_LOCAL_MACHINE, 'Software\Wow6432Node\Microsoft\VisualStudio\8.0', 'InstallDir');
end;

function IsVS2008Installed() : Boolean;
begin
   Result := RegValueExists(HKEY_LOCAL_MACHINE, 'Software\Microsoft\VisualStudio\9.0', 'InstallDir') or RegValueExists(HKEY_LOCAL_MACHINE, 'Software\Wow6432Node\Microsoft\VisualStudio\9.0', 'InstallDir');
end;

function IsVSNotInstalled() : Boolean;
begin
    Result := not (IsVS2005Installed() or IsVS2008Installed());
end;






